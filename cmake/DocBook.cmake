function(add_docbook)
  cmake_parse_arguments(DOCBOOK "" "TARGET;DOCUMENT" "SOURCES;DEPENDS" ${ARGN})

  get_filename_component(DOCBOOK_DOCUMENT_BASE_NAME "${DOCBOOK_DOCUMENT}" NAME_WE)
  message(STATUS "Create PDF from ${DOCBOOK_DOCUMENT} for target ${DOCBOOK_TARGET}")

  add_custom_command(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.pdf"
    COMMAND fop ARGS -fo "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.fo"
                     -pdf "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.pdf"
    DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.fo"
            ${DOCBOOK_DEPENDS}
    COMMENT "Compile ${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.pdf"
    VERBATIM)

  add_custom_command(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.fo"
    COMMAND xsltproc ARGS -o "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.fo"
                          "${CMAKE_CURRENT_BINARY_DIR}/docbook-fo.xslt"
                          "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.xml"
    DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/docbook-fo.xslt"
            "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.xml"
    COMMENT "Generate ${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.fo"
    VERBATIM)

  add_custom_command(OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.xml"
    COMMAND xsltproc ARGS -o "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.xml"
                          "${DocBookXSL_DIR}/assembly/assemble.xsl"
                          "${CMAKE_CURRENT_SOURCE_DIR}/${DOCBOOK_DOCUMENT}"
    DEPENDS ${DOCBOOK_DOCUMENT} ${DOCBOOK_SOURCES}
    COMMENT "Assemble ${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.xml"
    VERBATIM)

  add_custom_target(${DOCBOOK_TARGET} ALL
    DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/${DOCBOOK_DOCUMENT_BASE_NAME}.pdf"
            ${DOCBOOK_SOURCES} ${DOCBOOK_DEPENDS}
    SOURCES ${DOCBOOK_DOCUMENT} ${DOCBOOK_SOURCES})
endfunction()
